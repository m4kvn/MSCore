buildscript {
    ext.kotlin_version = '1.0.6'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = "com.MasahiroSaito.Spigot"
version = "0.5"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = "exposed-repo"
        url = "https://dl.bintray.com/kotlin/exposed/"
    }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    compile 'org.spigotmc:spigot-api:1.10.2-R0.1-SNAPSHOT'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    embed "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.h2database:h2:1.4.191'
    embed 'com.h2database:h2:1.4.191'
    compile 'org.jetbrains.exposed:exposed:0.7.5'
    embed 'org.jetbrains.exposed:exposed:0.7.5'
    compile 'org.slf4j:slf4j-log4j12:1.7.21'
    embed 'org.slf4j:slf4j-log4j12:1.7.21'
}

jar {
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
}

task sourceJar(type : Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier = 'sources'
                extension  = 'jar'
            }
        }
    }
}

bintray {
    user = project.hasProperty("bintrayUser") ? bintrayUser : ""
    key = project.hasProperty("bintrayKey") ? bintrayKey : ""
    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'Spigot-Plugin'
        name = "$project.name"
        userOrg = 'masahirosaito-repo'
        websiteUrl = "https://github.com/MasahiroSaito/$project.name"
        issueTrackerUrl = "https://github.com/MasahiroSaito/$project.name/issues"
        licenses = ['Apache-2.0']
        githubRepo = "MasahiroSaito/$project.name"
        githubReleaseNotesFile = 'README.md'
    }
}
